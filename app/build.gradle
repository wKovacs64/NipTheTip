apply plugin: 'com.android.application'

def gitHash = "git rev-parse --short HEAD".execute().text.trim()

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 102
        versionName '1.1.1-SNAPSHOT'
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        prod {
            applicationId 'com.wkovacs64.nipthetip.prod'
        }
        dev {
            applicationId 'com.wkovacs64.nipthetip.dev'
        }
    }
    applicationVariants.all { variant ->
        def flavor = variant.properties.get('flavorName')
        if (!(variant.buildType.name == 'release' && flavor == 'prod')) {
            // Append short hash of latest git commit if available
            if (!gitHash.isEmpty()) {
                variant.mergedFlavor.versionName += '+' + gitHash
            }
        }
    }
    lintOptions {
        abortOnError false
        disable 'GoogleAppIndexingWarning'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile('com.mikepenz:aboutlibraries:5.8.1') {
        exclude module: 'cardview-v7'
    }
    compile 'com.mikepenz:itemanimators:0.2.4@aar'
    compile 'com.mikepenz:iconics-core:2.8.1@aar'
    compile 'com.mikepenz:octicons-typeface:3.2.0.1@aar'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.4'
    compile('com.afollestad.material-dialogs:core:0.9.4.2@aar') {
        exclude module: 'support-v13'
        transitive = true
    }
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'frankiesardo:icepick:3.2.0'
    annotationProcessor 'frankiesardo:icepick-processor:3.2.0'
    compile 'com.google.dagger:dagger:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
}

// specify signing properties on the command line or environment variables
if (hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.keyPassword = keyPassword
    android.buildTypes.release.signingConfig = android.signingConfigs.release
} else {
    android.buildTypes.release.signingConfig = null
}
