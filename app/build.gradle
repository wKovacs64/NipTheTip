apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

def gitHash = "git rev-parse --short HEAD".execute().text.trim()

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 23
        versionCode 102
        versionName '1.0.2-SNAPSHOT'
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        prod {
            applicationId 'com.wkovacs64.nipthetip.prod'
        }
        dev {
            applicationId 'com.wkovacs64.nipthetip.dev'
        }
    }
    applicationVariants.all { variant ->
        def flavor = variant.properties.get('flavorName')
        if (!(variant.buildType.name == 'release' && flavor == 'prod')) {
            // Append short hash of latest git commit if available
            if (!gitHash.isEmpty()) {
                variant.mergedFlavor.versionName += '+' + gitHash
            }
        }
    }
    lintOptions {
        abortOnError false
        disable 'GoogleAppIndexingWarning'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:design:23.4.0'
    compile 'com.mikepenz:aboutlibraries:5.5.7'
    compile 'com.mikepenz:itemanimators:0.2.1@aar'
    compile 'com.mikepenz:iconics-core:2.5.7@aar'
    compile 'com.mikepenz:octicons-typeface:3.2.0.1@aar'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.4'
    compile('com.afollestad.material-dialogs:core:0.8.5.9@aar') {
        transitive = true
    }
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'frankiesardo:icepick:3.2.0'
    apt 'frankiesardo:icepick-processor:3.2.0'
    compile 'com.google.dagger:dagger:2.4'
    apt 'com.google.dagger:dagger-compiler:2.4'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-SNAPSHOT'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-SNAPSHOT'
}

// specify signing properties on the command line or environment variables
if (hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.keyPassword = keyPassword
    android.buildTypes.release.signingConfig = android.signingConfigs.release
} else {
    android.buildTypes.release.signingConfig = null
}
